# --------- Start of MagAnalysis -------------------------------------------------

# get scg sequences of all MAGs and concatenate them by scg.
rule Mags_SCG:
    input: mag_list="{group}/binning/{binner}/{binner}_MAG_list.txt",
           cluster_definition="{group}/binning/{binner}/clustering_{binner}.csv",
           cog_annotation='{group}/annotation/contigs_SCG.fna'
    output: expand("{{group}}/MagAnalysis/{{binner}}/SCGs/{COG}.fna",COG=LIST_COGS)
    params: "{group}/MagAnalysis/{binner}/SCGs/"
    shell: """
    {SCRIPTS}/Get_MAG_SCG.py {input.cog_annotation} {input.cluster_definition} {input.mag_list} {SCG_DATA}/scg_cogs_min0.97_max1.03_unique_genera.txt {params}
    """

# ------- if needed extract refference cogs -------------------
rule extract_ref_SCGs:
    input:SCG_DATA+"/All_COGs.tar.gz"
    output: expand(SCG_DATA+"/All_{COG}.ffn",COG=LIST_COGS)
    shell:"tar -xf {input} -C {SCG_DATA}/ "

# ------- Start allignment -------------------
rule run_mafft:
    input: scg="{path}/SCGs/{COG}.fna",
           database_scg=SCG_DATA+"/All_{COG}.ffn"
    output: all_scg="{path}/AlignAll/{COG}_all.fna",
            alignement="{path}/AlignAll/{COG}_all.msa"
    log: "{path}/AlignAll/mafft_{COG}.log"
    threads:1000
    shell: """
    cat {input.scg} {input.database_scg} > {output.all_scg}
    mafft --thread {threads} {output.all_scg} > {output.alignement} 2>{log}
    """

# ------- Trim allignment -------------------
rule trimal :
    input: "{path}/{COG}_all.msa"
    output: "{path}/{COG}_all_trim.msa"
    shell: "trimal -in {input} -out {output} -gt 0.9 -cons 60"

# ------- Fastree pre processing -------------
rule generate_data_for_fastree:
    input: trimed = expand("{{path}}/{cog}_all_trim.msa", cog = LIST_COGS),
           alignement = expand("{{path}}/{cog}_all.msa", cog = LIST_COGS)
    output: gfa = "{path}/concat_scg.msa",
            names = "{path}/Names.txt",
            temp = temp("{path}/temp_combine_genes.tmp")
    shell:"""
    cat {input.alignement} | grep ">" | sed 's/_COG.*//' | sort | uniq | sed 's/>//g' > {output.names}
    {SCRIPTS}/CombineGenes.pl {output.names} {input.trimed} > {output.temp}
    {SCRIPTS}/MapTI.pl {SCG_DATA}/TaxaSpeciesR.txt < {output.temp} > {output.gfa}
    """

# ----------- Launch Fastree ------------------
rule Launch_FastTree:
    input: "{path}/AlignAll/concat_scg.msa"
    output: "{path}/Tree/Mag_and_refseqs.tree"
    log: "{path}/AlignAll/SelectR.out"
    shell:"""
    FastTreeMP -nt -gtr < {input} 2> {log} > {output}
    """

# --------- Assign mag to lca with ref  ---------
rule  Assign_tree:
    input: "{path}/Mag_and_refseqs.tree"
    output: "{path}/Mag_refseq_assign.tsv"
    shell :"""
    {SCRIPTS}/AssignTree.py {input} {SCG_DATA}/TaxaSpeciesR.txt {SCG_DATA}/all_taxa_lineage.tsv > {output}
    """


# --------- MAG annotation -------------------------------------------------







